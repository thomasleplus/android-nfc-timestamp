// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.7.22"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.44.0'
}

allprojects {

    apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin

    repositories {
        google()
        jcenter()
    }
    
    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }
    
    // https://github.com/ben-manes/gradle-versions-plugin
    tasks.named("dependencyUpdates").configure {
      resolutionStrategy {
        componentSelection {
          all {
            if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
              reject('Release candidate')
            }
          }
        }
      }
    }
    
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
